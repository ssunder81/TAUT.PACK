*-----------------------------------------------------------------------------
* <Rating>-150</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE TAUT.OFS.RE.UPLOAD.TARGET

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.STANDARD.SELECTION
    $INSERT I_XML.EQUATE
    $INSERT I_GTS.COMMON
    $INCLUDE TAUT.BP I_F.TAUT.PARAMETER
    $INCLUDE TAUT.BP I_F.TAUT.APPL.DETAILS
    $INCLUDE TAUT.BP I_F.TAUT.APPL.CHANGE
    $INSERT I_GTS.COMMON
    $INSERT I_System

    GOSUB LOAD.VARIABLES
    GOSUB INITIALISE
    SEL.CMD= 'SELECT ':Y.TGT.UPL.REPEAT :" BY @ID"

    CALL EB.READLIST(SEL.CMD,ID.LIST,'','','')
    LOOP
        REMOVE OFS.QUEUE.ID FROM ID.LIST SETTING QUE.POS
    WHILE OFS.QUEUE.ID:QUE.POS

        GOSUB LOAD.OFS.MESSAGE
        GOSUB MAIN.PROCESS

    REPEAT

    IF R.FAILED.REPORT THEN
        WRITE R.FAILED.REPORT TO F.TGT.UPL.FAILED.REPORT,Y.TGT.UPL.FAILED.REPORT.ID
    END

    RETURN


LOAD.VARIABLES:
****************

    FN.SAVE.LIST  = "&SAVEDLISTS&"
    F.SAVE.LIST = ''
    CALL OPF(FN.SAVE.LIST,F.SAVE.LIST)

    FN.STANDARD.SELECTION = 'F.STANDARD.SELECTION'
    F.STANDARD.SELECTION = ''
    CALL OPF(FN.STANDARD.SELECTION,F.STANDARD.SELECTION)

    FN.TAUT.PARAM = 'F.TAUT.PARAMETER'
    F.TAUT.PARAM = ''
    CALL OPF(FN.TAUT.PARAM,F.TAUT.PARAM)

    FN.TAUT.APP.DET = 'F.TAUT.APPL.DETAILS'
    F.TAUT.APP.DET = ''
    CALL OPF(FN.TAUT.APP.DET,F.TAUT.APP.DET)

    FN.TAUT.APPL.CHANGE = 'F.TAUT.APPL.CHANGE'
    F.TAUT.APPL.CHANGE = ''
    CALL OPF(FN.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE)

    FN.TAUT.W.NEW.ID.REF = 'F.TAUT.W.NEW.ID.REF'
    F.TAUT.W.NEW.ID.REF = ''
    CALL OPF(FN.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF)

    FN.OFS.SOURCE = 'F.OFS.SOURCE'
    F.OFS.SOURCE = ''
    CALL OPF(FN.OFS.SOURCE,F.OFS.SOURCE)

    OFS.SRC.ID = "TAUT.OFS.UPLOAD"
    CALL F.READ(FN.OFS.SOURCE,OFS.SRC.ID,R.OFS.SOURCE,F.OFS.SOURCE,'')


    R.TAUT.APPL.CHANGE.SYS = ''
    CHG.READ.ERR = ''
    CALL F.READ(FN.TAUT.APPL.CHANGE,'SYSTEM',R.TAUT.APPL.CHANGE.SYS,F.TAUT.APPL.CHANGE,CHG.READ.ERR)

    R.TAUT.PARAM = ''
    TAUT.PARAM.READ.ERR = ''
    CALL F.READ(FN.TAUT.PARAM,'SYSTEM',R.TAUT.PARAM,F.TAUT.PARAM,TAUT.PARAM.READ.ERR)

    R.TAUT.APP.DET = ''
    TAUT.APP.DET.READ.ERR = ''
    CALL F.READ(FN.TAUT.APP.DET,'SYSTEM',R.TAUT.APP.DET,F.TAUT.APP.DET,TAUT.APP.DET.READ.ERR)

    RETURN

INITIALISE:
************
    IF R.TAUT.PARAM THEN
        GOSUB OPEN.TAUT.LOG.FILE
        GOSUB OPEN.TAUT.OFS.QUEUE

        TRANS.TYPE = R.TAUT.PARAM<TAUT.TRANS.TYPE>
        EXCL.FUNCTION = R.TAUT.PARAM<TAUT.EXCL.FUNCTION>
        EXCL.VERSION = R.TAUT.PARAM<TAUT.EXCL.VERSION>
        EXCL.APPL = R.TAUT.PARAM<TAUT.EXCL.APPL>
        EXCL.USER = R.TAUT.PARAM<TAUT.EXCL.USER>
        EXCL.COMPANY = R.TAUT.PARAM<TAUT.EXCL.COMPANY>
        TGT.T24.RELEASE = R.TAUT.PARAM<TAUT.TGT.T24.REL>
        SAMPLE.CNTR = R.TAUT.PARAM<TAUT.SAMPLE.CNTR>
        PRD.ONLINE.JNL = R.TAUT.PARAM<TAUT.PRD.ONLINE.JNL>
        JNL.FREQ = R.TAUT.PARAM<TAUT.ONLINE.JNL.FREQ>
        OFS.USER = R.TAUT.PARAM<TAUT.TGT.OFS.USER>
        OFS.PASSWD = R.TAUT.PARAM<TAUT.TGT.OFS.PASSWD>
        START.JNL = R.TAUT.PARAM<TAUT.START.JNL>
        END.JNL = R.TAUT.PARAM<TAUT.END.JNL>
        OFS.CONV.RTN = R.TAUT.PARAM<TAUT.OFS.CONV.RTN>
        UPDATE.RESP = R.TAUT.PARAM<TAUT.UPDATE.RESP>

    END

    IF R.TAUT.APP.DET THEN
        SPE.FILE.NAME = R.TAUT.APP.DET<TAUT.FILE.NAME>
        OFS.PRE.CONV.RTN = R.TAUT.APP.DET<TAUT.PRE.OFS.RTN>
        OFS.POST.CONV.RTN = R.TAUT.APP.DET<TAUT.POST.OFS.RTN>
        MESSAGE.SYNTAX = R.TAUT.APP.DET<TAUT.MESSAGE.SYNTAX>
        DELIM = R.TAUT.APP.DET<TAUT.MSG.DELIMITER>
        POST.MESSAGE = R.TAUT.APP.DET<TAUT.POST.MESSAGE>
    END

    Y.DELIM = ","
    RETURN

OPEN.TAUT.LOG.FILE:
*******************
    T.LOG.LEVEL = R.TAUT.PARAM<TAUT.LOG.LEVEL>

    IF T.LOG.LEVEL NE 'NONE' THEN
        LOG.FILE = R.TAUT.PARAM<TAUT.LOG.FILE>

        F.TAUT.LOG = ''
        OPEN LOG.FILE TO F.TAUT.LOG ELSE
            LOG.LEVEL = 1     ;* Fatal
            TAUT.TEXT = 'Cannot open the LOG file: ':LOG.FILE
            GOSUB UPDATE.TAUT.LOG
            RETURN
        END
    END

    IF TAUT.PARAM.READ.ERR OR R.TAUT.PARAM EQ '' THEN
        LOG.LEVEL = 1         ;* Fatal
        TAUT.TEXT = 'TAUT.PARAMETER RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    IF TAUT.APP.DET.READ.ERR OR R.TAUT.APP.DET EQ '' THEN
        LOG.LEVEL = 3         ;* Info
        TAUT.TEXT = 'TAUT.APPL.DETAILS RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
    END

    RETURN

OPEN.TAUT.OFS.QUEUE:
********************
    DATA.SYNC = R.TAUT.PARAM<TAUT.DATA.SYNC>

    IF DATA.SYNC EQ 'OFFLINE' THEN

        STORE.FWD.QUEUE = R.TAUT.PARAM<TAUT.STORE.FWD.QUEUE>
        F.STORE.FWD.QUEUE = ''
        CALL OPF(STORE.FWD.QUEUE,F.STORE.FWD.QUEUE)

        FN.TAUT.STORE.JOURNAL = R.TAUT.PARAM<TAUT.PROCESSED.JNL>
        F.TAUT.STORE.JOURNAL  = ''
        CALL OPF(FN.TAUT.STORE.JOURNAL,F.TAUT.STORE.JOURNAL)

        Y.TGT.UPL.REPEAT = R.TAUT.PARAM<TAUT.TGT.UPL.REPEAT>
        FN.TGT.UPL.REPEAT = Y.TGT.UPL.REPEAT
        F.TGT.UPL.REPEAT = ""
        CALL OPF(FN.TGT.UPL.REPEAT,F.TGT.UPL.REPEAT)

        Y.TGT.UPL.FAILED  = R.TAUT.PARAM<TAUT.TGT.UPL.FAILED>
        FN.TGT.UPL.FAILED = Y.TGT.UPL.FAILED
        F.TGT.UPL.FAILED  = ""
        CALL OPF(FN.TGT.UPL.FAILED,F.TGT.UPL.FAILED)

        Y.TGT.UPL.FAILED.REPORT = R.TAUT.PARAM<TAUT.TGT.UPL.FAILED.REPORT>

        FN.TGT.UPL.FAILED.REPORT = Y.TGT.UPL.FAILED.REPORT
        F.TGT.UPL.FAILED.REPORT = ""

        Y.TGT.UPLOAD.DONE = R.TAUT.PARAM<TAUT.TGT.UPLOAD.DONE>

        FN.TGT.UPLOAD.DONE = Y.TGT.UPLOAD.DONE
        F.TGT.UPLOAD.DONE = ""

        CALL OPF(FN.TGT.UPLOAD.DONE,F.TGT.UPLOAD.DONE)

        CALL OPF(FN.TGT.UPL.FAILED.REPORT,F.TGT.UPL.FAILED.REPORT)
        Y.TIME.DATE = TIMEDATE()
        CONVERT ":" TO "-" IN Y.TIME.DATE
        Y.TGT.UPL.FAILED.REPORT.ID = Y.TIME.DATE:"_Failed upload transaction.csv"

    END
    SOURCE.FILE = R.TAUT.PARAM<TAUT.SOURCE.FILE>
    RETURN

LOAD.OFS.MESSAGE:
******************
    LOG.LEVEL = ''
    GOSUB READ.OFS.QUEUE
    SOURCE.OFS.ARR = R.TGT.UPL.REPEAT
    CONVERT ',' TO FM IN SOURCE.OFS.ARR
    SRC.OFS.MESSAGE = R.TGT.UPL.REPEAT
    OFS.APPLICATION.NAME = FIELD(OFS.QUEUE.ID,'_',2)

    LOCATE OFS.APPLICATION.NAME IN R.TAUT.APPL.CHANGE.SYS<TAUT.APC.OB.APPLICATION,1> SETTING POS THEN
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'This Application has been made OB: ':OFS.APPLICATION.NAME
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    TR.OFS.SS.REC = ''
    CALL GET.STANDARD.SELECTION.DETS(OFS.APPLICATION.NAME,TR.OFS.SS.REC)
    TR.FIELD.NAME.LIST = TR.OFS.SS.REC<SSL.SYS.FIELD.NAME>
    TR.SYS.TYPE.LIST   = TR.OFS.SS.REC<SSL.SYS.TYPE>
    TR.SYS.VAL.PROG.LIST   = TR.OFS.SS.REC<SSL.SYS.VAL.PROG>

    R.TAUT.APPL.CHANGE = ''
    CALL F.READ(FN.TAUT.APPL.CHANGE,OFS.APPLICATION.NAME,R.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE,CHG.READ.ERR)

    RETURN

READ.OFS.QUEUE:
***************
    READ R.TGT.UPL.REPEAT FROM F.TGT.UPL.REPEAT,OFS.QUEUE.ID ELSE
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Cannot Find the TGT.UPL.REPEAT Record: ':OFS.QUEUE.ID
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    READ R.TAUT.OFS.TARGET FROM F.TAUT.STORE.JOURNAL,OFS.QUEUE.ID ELSE
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Cannot Find the JNL Record: ':OFS.QUEUE.ID
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    RETURN

MAIN.PROCESS:
*------------
    G.SPARE1 = OFS.QUEUE.ID
    CALL System.setVariable("MASTER.JNL.ID",OFS.QUEUE.ID)

    GOSUB SRC.OFS.FIELD.CHECK
    GOSUB TG.MANDATORY.FIELD.CHECK
    GOSUB CALL.OFS.BULK.MANAGER


    RETURN

SRC.OFS.FIELD.CHECK:
********************
    TARGET.FIELDS.OB = ''
    SRC.OFS.FIELD.NAME.ARR = ''
    TARGET.OFS.FIELD.NAME.ARR = ''
    NO.FIELDS = DCOUNT(SRC.OFS.MESSAGE,',')
    TARGET.OFS = FIELDS(SRC.OFS.MESSAGE,',',1,4)
    FOR IDX.NO = 5 TO NO.FIELDS
        LOC.VM.POS = 0
        SRC.OFS.FIELD.NAME = FIELD(FIELD(SRC.OFS.MESSAGE,',',IDX.NO),":",1)
        SRC.OFS.FIELD.NAME.ARR<-1> =  SRC.OFS.FIELD.NAME

        LOCATE SRC.OFS.FIELD.NAME IN TR.FIELD.NAME.LIST<1,1> SETTING LOC.VM.POS THEN
            TARGET.OFS.FIELD.NAME.ARR<1,-1> = SRC.OFS.FIELD.NAME
            TARGET.OFS := ',':FIELD(SRC.OFS.MESSAGE,',',IDX.NO)
        END ELSE
            TARGET.FIELDS.OB<1,-1> = FIELD(FIELD(SRC.OFS.MESSAGE,',',IDX.NO),":",1)
        END
    NEXT IDX.NO

    IF TARGET.FIELDS.OB THEN
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Fields Missing in this Application (Might be Removed): ': OFS.APPLICATION.NAME
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    RETURN

TG.MANDATORY.FIELD.CHECK:
*************************

    NO.OF.MD.FIELD = DCOUNT(R.TAUT.APPL.CHANGE<TAUT.APC.MANDATORY.FIELDS>,VM)
    FOR MPOS=1 TO NO.OF.MD.FIELD
        MD.FIELD.NAME = R.TAUT.APPL.CHANGE<TAUT.APC.MANDATORY.FIELDS,MPOS>
        LOCATE MD.FIELD.NAME IN SRC.OFS.FIELD.NAME.ARR SETTING MFOUND.POS ELSE
            TARGET.OFS := ',': MD.FIELD.NAME :"::=":R.TAUT.APPL.CHANGE<TAUT.APC.FIELD.VALUE,MPOS>
        END
    NEXT MPOS

    RETURN

CALL.OFS.BULK.MANAGER:
**********************

    IF TARGET.OFS THEN
*      SAVE.APP = APPLICATION
*      SAVE.V = V
*     OFS.SRC.ID.BACKUP = OFS$SOURCE.ID
*     OFS.SRC.REC.BACKUP = OFS$SOURCE.REC

*    OFS$SOURCE.ID = OFS.SRC.ID
*   OFS$SOURCE.REC = R.OFS.SOURCE

        Y.APPL = FIELD(TARGET.OFS,',',1)
        Y.AA.INP.FLAG = ''
*        IF Y.APPL EQ 'AA.ARRANGEMENT.ACTIVITY' THEN

        Y.FUN = FIELD(OFS.QUEUE.ID,'_',4)
        IF Y.FUN EQ 'I' THEN
            Y.AA.INP.FLAG = 1
            Y.AAA.REF.ID = FIELD(OFS.QUEUE.ID,'_',3)
            CALL F.READ(FN.TAUT.W.NEW.ID.REF, Y.AAA.REF.ID, R.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF, Y.TAUT.ERR)
        END
        IF Y.FUN EQ 'A' OR Y.FUN EQ 'D' THEN
            Y.AAA.REF.ID = FIELD(OFS.QUEUE.ID,'_',3)
            CALL F.READ(FN.TAUT.W.NEW.ID.REF, Y.AAA.REF.ID, R.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF, Y.TAUT.ERR)
            TARGET.OFS:=R.TAUT.W.NEW.ID.REF
        END
*       END

        theRequests = TARGET.OFS
        theResponses = ''
        OPERATOR = "AMAR01"
        Y.OFS.SRC.ID = "DUMMY":FM:"OFS"

        CALL OFS.NEW.TRANSACTION(Y.OFS.SRC.ID, theRequests, theResponses)

*        requestCommitted = ''
*        CALL OFS.BULK.MANAGER(theRequests, theResponses, requestCommitted)

*        Y.OFS.MSG = TARGET.OFS

*        CALL OFS.GLOBUS.MANAGER(OFS.SRC.ID,Y.OFS.MSG)
*        theResponses = Y.OFS.MSG

*        APPLICATION = SAVE.APP
*        V = SAVE.V
*        OFS$SOURCE.ID  = OFS.SRC.ID.BACKUP
*        OFS$SOURCE.REC = OFS.SRC.REC.BACKUP

        Y.OFS.MSG.ERROR.PART = theResponses['/',3,1]
        ERROR.FLAG = Y.OFS.MSG.ERROR.PART[',',1,1]
        OFS.TXN.ID   = FIELD(theResponses,'/',1)

        IF Y.AA.INP.FLAG EQ '1' THEN
            Y.AAA.ID.REF = theResponses['/',1,1]
*            Y.AAA.ID = Y.AAA.ID.REF[20,20]
            R.TAUT.W.NEW.ID.REF = Y.AAA.ID.REF
            CALL F.WRITE(FN.TAUT.W.NEW.ID.REF, Y.AAA.REF.ID, R.TAUT.W.NEW.ID.REF)
        END


*        IF requestCommitted NE 1 OR ERROR.FLAG = -1 THEN
        IF ERROR.FLAG = -1 THEN
            TAUT.TEXT = 'OFS Message Failed' :" * ": TARGET.OFS
            GOSUB UPDATE.TAUT.LOG
            GOSUB UPDATE.TGT.UPL.REPEAT
            GOSUB UPDATE.FAILED.REPORT

        END ELSE
            WRITE SRC.OFS.MESSAGE TO F.TGT.UPLOAD.DONE,OFS.QUEUE.ID
            DELETE F.TGT.UPL.REPEAT,OFS.QUEUE.ID
            DELETE F.TGT.UPL.FAILED,OFS.QUEUE.ID
        END

    END
    RETURN

UPDATE.TAUT.LOG:
****************
    YID = ''
    CALL ALLOCATE.UNIQUE.TIME(YID)
    IF TAUT.TEXT THEN
        IF T.LOG.LEVEL EQ 'FULL' THEN
            BEGIN CASE
            CASE LOG.LEVEL = 1
                TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 2
                TAUT.TEXT = "Warning  :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 3
                TAUT.TEXT = "Info :" :TIMEDATE():" ":TAUT.TEXT
            END CASE

            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END

        IF T.LOG.LEVEL EQ 'FATAL' AND LOG.LEVEL EQ 1 THEN
            TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END
    END

    TAUT.TEXT = ''
    RETURN

UPDATE.TGT.UPL.REPEAT:
**********************
* Move the failed cases to TGT.UPL.REPEAT folder to reprocess and delete the message from STORE.FWD.QUEUE.

    WRITE SRC.OFS.MESSAGE TO  F.TGT.UPL.REPEAT,OFS.QUEUE.ID
    DELETE F.STORE.FWD.QUEUE,OFS.QUEUE.ID
    RETURN

UPDATE.FAILED.REPORT:
*********************

    Y.APPL.NAME = OFS.APPLICATION.NAME
    Y.OFS.ID = OFS.QUEUE.ID
    Y.APP.ID = OFS.TXN.ID
    Y.ORD.ID = FIELD(theResponses,"/",2,1)
    Y.FAILED.REASON = FIELD(theResponses,",",2,1)

    IF R.FAILED.REPORT = "" THEN
        R.FAILED.REPORT = "Application name":Y.DELIM:"OFS id":Y.DELIM:"Target Application ID":Y.DELIM:"ORD ID":Y.DELIM:"Failed Reason"
    END ELSE
        R.FAILED.REPORT<-1> = Y.APPL.NAME:Y.DELIM:Y.OFS.ID:Y.DELIM:Y.APP.ID:Y.DELIM:Y.ORD.ID:Y.DELIM:Y.FAILED.REASON
    END

    RETURN

END

