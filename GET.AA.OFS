*-----------------------------------------------------------------------------
* <Rating>641</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE GET.AA.OFS(Y.ACT.ID, Y.OFS.STR, Y.MASTER.JNL.ID, Y.ERROR)

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.PROCESS.DETAILS
    $INSERT I_F.VERSION
    $INSERT I_F.AA.ARRANGEMENT.ACTIVITY
    $INSERT I_F.STANDARD.SELECTION
    $INSERT I_F.AA.ACTIVITY.HISTORY
    $INSERT I_F.AA.ARRANGEMENT

    GOSUB OPEN.FILES
    GOSUB PROCESS

    RETURN

OPEN.FILES:
*---------
    FN.VERSION = 'F.VERSION'
    F.VERSION = ''
    CALL OPF(FN.VERSION, F.VERSION)

    FN.AA.PROCESS.DETAILS = 'F.AA.PROCESS.DETAILS'
    F.AA.PROCESS.DETAILS = ''
    CALL OPF(FN.AA.PROCESS.DETAILS, F.AA.PROCESS.DETAILS)

    FN.AA.ACTIVITY.HISTORY = 'F.AA.ACTIVITY.HISTORY'
    F.AA.ACTIVITY.HISTORY = ''
    CALL OPF(FN.AA.ACTIVITY.HISTORY, F.AA.ACTIVITY.HISTORY)

    FN.AA.ARRANGEMENT.ACTIVITY = 'F.AA.ARRANGEMENT.ACTIVITY'
    F.AA.ARRANGEMENT.ACTIVITY = ''
    CALL OPF(FN.AA.ARRANGEMENT.ACTIVITY, F.AA.ARRANGEMENT.ACTIVITY)

    FN.AA.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT = ''
    CALL OPF(FN.AA.ARRANGEMENT, F.AA.ARRANGEMENT)

    RETURN

PROCESS:
*-------

    Y.APPLICATION = FIELD(Y.MASTER.JNL.ID,'_',2)
    Y.FUNCTION = FIELD(Y.MASTER.JNL.ID,'_',4)
    Y.VERS = FIELD(Y.MASTER.JNL.ID,'_',5)
    Y.VERSION1 = 'AA.ARRANGEMENT.ACTIVITY,TAUT'
    IF Y.FUNCTION EQ 'A' THEN
        Y.OFS.STR = Y.VERSION1:'/':Y.FUNCTION:'/PROCESS,//':ID.COMPANY:','
        RETURN
    END
    CALL F.READ(FN.AA.ARRANGEMENT.ACTIVITY, Y.ACT.ID, R.AA.ARRANGEMENT.ACTIVITY, F.AA.ARRANGEMENT.ACTIVITY, Y.AA.ARR.ERR)
    IF NOT(R.AA.ARRANGEMENT.ACTIVITY) THEN
        CALL F.READ(FN.AA.ACTIVITY.HISTORY, Y.ACT.ID, R.AA.ACTIVITY.HISTORY, F.AA.ACTIVITY.HISTORY, Y.ARR.ERR)
        Y.EFF.DATE.TOT = R.AA.ACTIVITY.HISTORY<AA.AH.EFFECTIVE.DATE>
        Y.ACT.TOT = R.AA.ACTIVITY.HISTORY<AA.AH.ACTIVITY>
        FINDSTR "ACCOUNTS-NEW-ARRANGEMENT" IN Y.ACT.TOT SETTING Y.FM.POS, Y.DAT.POS, Y.SM.POS THEN
            Y.ACT.REF = R.AA.ACTIVITY.HISTORY<AA.AH.ACTIVITY.REF, Y.DAT.POS,Y.SM.POS>
            CALL F.READ(FN.AA.ARRANGEMENT.ACTIVITY, Y.ACT.REF, R.AA.ARRANGEMENT.ACTIVITY, F.AA.ARRANGEMENT.ACTIVITY, Y.AA.ARR.ERR)
            GOSUB PROCESS.ACT.DETAILS
        END
        FINDSTR "DEPOSITS-NEW-ARRANGEMENT" IN Y.ACT.TOT SETTING Y.FM.POS, Y.DAT.POS, Y.SM.POS THEN
            Y.ACT.REF = R.AA.ACTIVITY.HISTORY<AA.AH.ACTIVITY.REF, Y.DAT.POS,Y.SM.POS>
            CALL F.READ(FN.AA.ARRANGEMENT.ACTIVITY, Y.ACT.REF, R.AA.ARRANGEMENT.ACTIVITY, F.AA.ARRANGEMENT.ACTIVITY, Y.AA.ARR.ERR)
            GOSUB PROCESS.ACT.DETAILS
        END

    END ELSE
        GOSUB PROCESS.ACT.DETAILS
    END
    RETURN

PROCESS.ACT.DETAILS:
*-------------------

    Y.INIT.TYPE = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.INITIATION.TYPE>
    IF Y.INIT.TYPE EQ 'USER' THEN
        Y.MASTER.ID = Y.ACT.ID
    END ELSE
        Y.MASTER.ID = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.MASTER.AAA>
        READ R.AA.ARRANGEMENT.ACTIVITY FROM F.AA.ARRANGEMENT.ACTIVITY ,Y.MASTER.ID ELSE Y.AAA.ERR=1
    END
    IF NOT(Y.MASTER.ID) THEN
        Y.ERROR = "ERROR ON ID"
        RETURN
    END
    Y.PROP.CNT = 1
    GOSUB FORM.OFS.HEAD
    Y.OFS.MSG = ''
    CALL F.READ(FN.AA.PROCESS.DETAILS, Y.MASTER.ID, R.AA.PROCESS.DETAILS, F.AA.PROCESS.DETAILS, Y.ERR)
    Y.PROCESS.DETAILS = R.AA.PROCESS.DETAILS<AA.PROC.PROCESS.DETAILS>
    Y.TOT.CNT = DCOUNT(Y.PROCESS.DETAILS,VM)
    CHANGE VM TO SM IN Y.PROCESS.DETAILS
    I = 1
    LOOP
    WHILE I LE Y.TOT.CNT
        Y.PROPERTY = R.AA.PROCESS.DETAILS<AA.PROC.PROPERTY><1,I>
        Y.OFS.PROP.MSG = ",PROPERTY:":Y.PROP.CNT:"=":Y.PROPERTY
        Y.DETAILS = Y.PROCESS.DETAILS<1,1,I>
        CHANGE ' ' TO VM IN Y.DETAILS
        Y.VERSION = Y.DETAILS<1,1>
        Y.FUNCTION = Y.DETAILS<1,2>
        CALL F.READ(FN.VERSION, Y.VERSION, R.VERSION1, F.VERSION, Y.ERR.VER)
        Y.FIELD.TOT = R.VERSION1<EB.VER.FIELD.NO>
        Y.NO.INPUT.FIELDS = R.VERSION1<EB.VER.NOINPUT.FIELD>
        Y.ASS.VERSION = R.VERSION1<EB.VER.ASSOC.VERSION>
        Y.ID = Y.DETAILS<1,3>
        IF Y.FUNCTION EQ 'I' THEN
            Y.APPL = FIELDS(Y.VERSION,',',1)
            Y.PROPERTY.CLASS = FIELDS(Y.APPL,'.',3,8)
*                        GOSUB GET.PROPERTY.PREFIX
            Y.PROPERTY.CLASS = 'AA.ARR.':Y.PROPERTY.CLASS

            FN.PROPERTY.CLASS = 'F.':Y.PROPERTY.CLASS
            F.PROPERTY.CLASS = ''
            CALL OPF(FN.PROPERTY.CLASS, F.PROPERTY.CLASS)

*                        CALL F.READ(FN.PROPERTY.CLASS, Y.ID, R.ARRAY, F.PROPERTY.CLASS, Y.PRP.ERR)
            READ R.ARRAY FROM F.PROPERTY.CLASS,Y.ID ELSE Y.PRP.ERR=1
* idArrangementComp = Y.ARR.ID
* idProperty = R.AA.PROCESS.DETAILS<AA.PROC.PROPERTY><1,I>
* effectiveDate = TODAY
* CALL AA.GET.ARRANGEMENT.CONDITIONS(idArrangementComp, idPropertyClass, idProperty, effectiveDate, returnIds, R.PROPERTY.CLASS.REC, returnError)
* R.ARRAY = RAISE(R.PROPERTY.CLASS.REC)

            APPL.NAME = Y.PROPERTY.CLASS
            FIELD.POS = 'CHANGED.FIELDS'
            CALL EB.GET.APPL.FIELD(APPL.NAME,FIELD.POS,APPL.SS,ERR.MSG)
            Y.C.FIELD.NAME = FIELD.POS

            Y.CHANGED.FIELDS.TOT = R.ARRAY<FIELD.POS>
            Y.CHANGED.FIELD.CNT = DCOUNT(Y.CHANGED.FIELDS.TOT,VM)
            Y.CHG.FLAG = ''
            Y.FIELD.CNT = 1
            Y.OFS.FIELD.MSG = ''
            K = 1
            LOOP
            WHILE K LE Y.CHANGED.FIELD.CNT
                Y.CHANGED.FIELD = Y.CHANGED.FIELDS.TOT<1,K>
                IF NOT(Y.ASS.VERSION) THEN
                    FINDSTR Y.CHANGED.FIELD IN Y.FIELD.TOT SETTING Y.F.FM, Y.F.VM, Y.F.SM THEN
                        LOCATE Y.CHANGED.FIELD IN Y.NO.INPUT.FIELDS<1,1> SETTING Y.NO.POS THEN
                        END ELSE
                            GOSUB CHECK.CHANGED.FIELDS
                        END
                    END
                END ELSE
                    GOSUB CHECK.CHANGED.FIELDS
                END
                K++
            REPEAT
            IF Y.CHG.FLAG THEN
                Y.OFS.MSG := Y.OFS.PROP.MSG:Y.OFS.FIELD.MSG
                Y.PROP.CNT +=1
            END
        END
        I++
    REPEAT

*END
    Y.OFS.STR = R.AA:Y.OFS.MSG
    CRT Y.OFS.STR
*    Y.REF.CNT++
*REPEAT

    RETURN

CHECK.CHANGED.FIELDS:
*-------------------

    Y.SM = 1
    FIELD.POS = Y.CHANGED.FIELD
    CALL EB.GET.APPL.FIELD(APPL.NAME,FIELD.POS,APPL.SS,ERR.MSG)
    Y.FIELD.NAME= FIELD.POS
    FINDSTR Y.CHANGED.FIELD IN APPL.SS SETTING  Y.FM, Y.VM, Y.SM THEN
        Y.FIELD.TYPE = APPL.SS<SSL.SYS.VAL.PROG, Y.VM, Y.SM>
        Y.CHK.INP = FIELD(Y.FIELD.TYPE,'&',3)
    END
    IF Y.CHK.INP NE 'NOINPUT' THEN
        Y.FIELD.VAL.TOT = R.ARRAY<Y.FIELD.NAME>
        GOSUB FORM.OFS.MSG
    END
    RETURN

FORM.OFS.MSG:
*------------
    Y.VM = 1
    Y.VM.CNT = DCOUNT(Y.FIELD.VAL.TOT,VM)
    LOOP
    WHILE Y.VM LE Y.VM.CNT
        Y.FIELD.VAL.VM = Y.FIELD.VAL.TOT<1,Y.VM>
        Y.SM = 1
        Y.SM.CNT = DCOUNT(Y.FIELD.VAL.VM,SM)
        LOOP
        WHILE Y.SM LE Y.SM.CNT
            Y.CHG.FLAG = 1
            Y.FIELD.VAL = Y.FIELD.VAL.VM<1,1,Y.SM>
            Y.OFS.FIELD.MSG:=",FIELD.NAME:":Y.PROP.CNT:":":Y.FIELD.CNT:"=":Y.CHANGED.FIELD:":":Y.VM:':':Y.SM:",FIELD.VALUE:":Y.PROP.CNT:":":Y.FIELD.CNT:"=":Y.FIELD.VAL
            Y.FIELD.CNT++
            Y.SM++
        REPEAT
        Y.VM++
    REPEAT

    RETURN
FORM.OFS.HEAD:
*------------
*   CALL F.READ(FN.AA.ARRANGEMENT.ACTIVITY, Y.MASTER.ID, R.AA.ARRANGEMENT.ACTIVITY, F.AA.ARRANGEMENT.ACTIVITY, Y.AAA.ERR)
*    READ R.AA.ARRANGEMENT.ACTIVITY FROM F.AA.ARRANGEMENT.ACTIVITY ,Y.MASTER.ID ELSE Y.AAA.ERR=1


    R.AA = Y.VERSION1:'/':Y.FUNCTION:'/PROCESS,//':ID.COMPANY:',,'

    Y.ACTY = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ACTIVITY>

    IF Y.ACTY EQ 'LENDING-NEW-ARRANGEMENT' OR Y.ACTY EQ 'DEPOSITS-NEW-ARRANGEMENT' OR Y.ACTY EQ 'ACCOUNTS-NEW-ARRANGEMENT' THEN
        Y.ARR.ID = '"NEW"'
        R.AA:='CUSTOMER:1:1=':R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.CUSTOMER>:','
        R.AA:='PRODUCT:1:1=':R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.PRODUCT>:','
        R.AA:='CURRENCY:1:1=':R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.CURRENCY>:','
    END ELSE
        Y.ARR.ID = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ARRANGEMENT>
    END

    R.AA:= 'ACTIVITY:1:1=':Y.ACTY:','
    R.AA:= 'ARRANGEMENT:1:1=':Y.ARR.ID

    RETURN
END
