*-----------------------------------------------------------------------------
* <Rating>1015</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE TAUT.OFS.UPLOAD.TARGET

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.STANDARD.SELECTION
    $INSERT I_XML.EQUATE
    $INSERT I_GTS.COMMON
    $INCLUDE TAUT.BP I_F.TAUT.PARAMETER
    $INCLUDE TAUT.BP I_F.TAUT.APPL.DETAILS
    $INCLUDE TAUT.BP I_F.TAUT.APPL.CHANGE
    $INSERT I_F.AA.PROPERTY
    $INCLUDE TAUT.BP I_TAUT.OFS.UPLOAD.TARGET.COMMON
    $INSERT I_System

    GOSUB LOAD.VARIABLES
    GOSUB INITIALISE

    PRINT "Selecting Records to process message from Source to Target"
    PRINT "----------------------------------------------------------"
    PRINT ""

    SEL.CMD= 'SELECT ':STORE.FWD.QUEUE :" BY @ID"

    Y.PROC.CNT = 1
    CALL EB.READLIST(SEL.CMD,ID.LIST,'','','')

    Y.TOT.CNT = DCOUNT(ID.LIST,FM)

    LOOP
        REMOVE OFS.QUEUE.ID FROM ID.LIST SETTING QUE.POS
    WHILE OFS.QUEUE.ID:QUE.POS


        PRINT Y.PROC.CNT
        PRINT OFS.QUEUE.ID
        PRINT QUE.POS

        PRINT "Processing ":Y.PROC.CNT:" OF ": Y.TOT.CNT

        GOSUB LOAD.OFS.MESSAGE
        GOSUB MAIN.PROCESS

        Y.PROC.CNT +=1

    REPEAT

    IF R.FAILED.REPORT THEN

        Y.TGT.UPL.FAILED.REPORT = R.TAUT.PARAM<TAUT.TGT.UPL.FAILED.REPORT>

        FN.TGT.UPL.FAILED.REPORT = Y.TGT.UPL.FAILED.REPORT
        F.TGT.UPL.FAILED.REPORT = ""
        CALL OPF(FN.TGT.UPL.FAILED.REPORT,F.TGT.UPL.FAILED.REPORT)

        PRINT ""
        PRINT "Error Report " : Y.TGT.UPL.FAILED.REPORT.ID : " written to the folder " : Y.TGT.UPL.FAILED.REPORT

        WRITE R.FAILED.REPORT TO F.TGT.UPL.FAILED.REPORT,Y.TGT.UPL.FAILED.REPORT.ID
    END

    IF R.TAUT.UPLD.DONE.RPT THEN

        Y.TGT.UPL.DONE.RPT = R.TAUT.PARAM<TAUT.TGT.UPL.DONE.RPT>

        FN.TGT.UPL.DONE.RPT= Y.TGT.UPL.DONE.RPT
        F.TGT.UPL.DONE.RPT = ""
        CALL OPF(FN.TGT.UPL.DONE.RPT,F.TGT.UPL.DONE.RPT)

        PRINT ""
        PRINT "Processed  Report ":Y.TGT.UPL.DONE.RPT.ID:" written to the folder ":Y.TGT.UPL.DONE.RPT

        WRITE R.TAUT.UPLD.DONE.RPT TO F.TGT.UPL.DONE.RPT,Y.TGT.UPL.DONE.RPT.ID
    END

    RETURN


LOAD.VARIABLES:
****************

    FN.SAVE.LIST  = "&SAVEDLISTS&"
    F.SAVE.LIST = ''
    CALL OPF(FN.SAVE.LIST,F.SAVE.LIST)

    FN.STANDARD.SELECTION = 'F.STANDARD.SELECTION'
    F.STANDARD.SELECTION = ''
    CALL OPF(FN.STANDARD.SELECTION,F.STANDARD.SELECTION)

    FN.TAUT.PARAM = 'F.TAUT.PARAMETER'
    F.TAUT.PARAM = ''
    CALL OPF(FN.TAUT.PARAM,F.TAUT.PARAM)

    FN.TAUT.APP.DET = 'F.TAUT.APPL.DETAILS'
    F.TAUT.APP.DET = ''
    CALL OPF(FN.TAUT.APP.DET,F.TAUT.APP.DET)

    FN.TAUT.APPL.CHANGE = 'F.TAUT.APPL.CHANGE'
    F.TAUT.APPL.CHANGE = ''
    CALL OPF(FN.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE)

    FN.TAUT.W.NEW.ID.REF = 'F.TAUT.W.NEW.ID.REF'
    F.TAUT.W.NEW.ID.REF = ''
    CALL OPF(FN.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF)

    FN.OFS.SOURCE = 'F.OFS.SOURCE'
    F.OFS.SOURCE = ''
    CALL OPF(FN.OFS.SOURCE,F.OFS.SOURCE)

    OFS.SRC.ID = "TAUT.OFS.UPLOAD"
    CALL F.READ(FN.OFS.SOURCE,OFS.SRC.ID,R.OFS.SOURCE,F.OFS.SOURCE,'')


    R.TAUT.APPL.CHANGE.SYS = ''
    CHG.READ.ERR = ''
    CALL F.READ(FN.TAUT.APPL.CHANGE,'SYSTEM',R.TAUT.APPL.CHANGE.SYS,F.TAUT.APPL.CHANGE,CHG.READ.ERR)

    R.TAUT.PARAM = ''
    TAUT.PARAM.READ.ERR = ''
    CALL F.READ(FN.TAUT.PARAM,'SYSTEM',R.TAUT.PARAM,F.TAUT.PARAM,TAUT.PARAM.READ.ERR)

    R.TAUT.APP.DET = ''
    TAUT.APP.DET.READ.ERR = ''
    CALL F.READ(FN.TAUT.APP.DET,'SYSTEM',R.TAUT.APP.DET,F.TAUT.APP.DET,TAUT.APP.DET.READ.ERR)

    FN.AA.PROPERTY = 'F.AA.PROPERTY'
    F.AA.PROPERTY = ''
    CALL OPF(FN.AA.PROPERTY, F.AA.PROPERTY)
    RETURN

INITIALISE:
************
    IF R.TAUT.PARAM THEN
        GOSUB OPEN.TAUT.LOG.FILE
        GOSUB OPEN.TAUT.OFS.QUEUE

        TRANS.TYPE = R.TAUT.PARAM<TAUT.TRANS.TYPE>
        EXCL.FUNCTION = R.TAUT.PARAM<TAUT.EXCL.FUNCTION>
        EXCL.VERSION = R.TAUT.PARAM<TAUT.EXCL.VERSION>
        EXCL.APPL = R.TAUT.PARAM<TAUT.EXCL.APPL>
        EXCL.USER = R.TAUT.PARAM<TAUT.EXCL.USER>
        EXCL.COMPANY = R.TAUT.PARAM<TAUT.EXCL.COMPANY>
        TGT.T24.RELEASE = R.TAUT.PARAM<TAUT.TGT.T24.REL>
        SAMPLE.CNTR = R.TAUT.PARAM<TAUT.SAMPLE.CNTR>
        PRD.ONLINE.JNL = R.TAUT.PARAM<TAUT.PRD.ONLINE.JNL>
        JNL.FREQ = R.TAUT.PARAM<TAUT.ONLINE.JNL.FREQ>
        OFS.USER = R.TAUT.PARAM<TAUT.TGT.OFS.USER>
        OFS.PASSWD = R.TAUT.PARAM<TAUT.TGT.OFS.PASSWD>
        START.JNL = R.TAUT.PARAM<TAUT.START.JNL>
        END.JNL = R.TAUT.PARAM<TAUT.END.JNL>
        OFS.CONV.RTN = R.TAUT.PARAM<TAUT.OFS.CONV.RTN>
        UPDATE.RESP = R.TAUT.PARAM<TAUT.UPDATE.RESP>

    END

    IF R.TAUT.APP.DET THEN
        SPE.FILE.NAME = R.TAUT.APP.DET<TAUT.FILE.NAME>
        OFS.PRE.CONV.RTN = R.TAUT.APP.DET<TAUT.PRE.OFS.RTN>
        OFS.POST.CONV.RTN = R.TAUT.APP.DET<TAUT.POST.OFS.RTN>
        MESSAGE.SYNTAX = R.TAUT.APP.DET<TAUT.MESSAGE.SYNTAX>
        DELIM = R.TAUT.APP.DET<TAUT.MSG.DELIMITER>
        POST.MESSAGE = R.TAUT.APP.DET<TAUT.POST.MESSAGE>
    END

    Y.DELIM = ","
    R.TAUT.UPLD.DONE.RPT = ""
    R.FAILED.REPORT = ""
    RETURN

OPEN.TAUT.LOG.FILE:
*******************
    T.LOG.LEVEL = R.TAUT.PARAM<TAUT.LOG.LEVEL>

    IF T.LOG.LEVEL NE 'NONE' THEN
        LOG.FILE = R.TAUT.PARAM<TAUT.LOG.FILE>

        F.TAUT.LOG = ''
        OPEN LOG.FILE TO F.TAUT.LOG ELSE
            LOG.LEVEL = 1     ;* Fatal
            TAUT.TEXT = 'Cannot open the LOG file: ':LOG.FILE
            GOSUB UPDATE.TAUT.LOG
            RETURN
        END
    END

    IF TAUT.PARAM.READ.ERR OR R.TAUT.PARAM EQ '' THEN
        LOG.LEVEL = 1         ;* Fatal
        TAUT.TEXT = 'TAUT.PARAMETER RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    IF TAUT.APP.DET.READ.ERR OR R.TAUT.APP.DET EQ '' THEN
        LOG.LEVEL = 3         ;* Info
        TAUT.TEXT = 'TAUT.APPL.DETAILS RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
    END

    RETURN

OPEN.TAUT.OFS.QUEUE:
********************
    DATA.SYNC = R.TAUT.PARAM<TAUT.DATA.SYNC>

    IF DATA.SYNC EQ 'OFFLINE' THEN

        STORE.FWD.QUEUE = R.TAUT.PARAM<TAUT.STORE.FWD.QUEUE>
        F.STORE.FWD.QUEUE = ''
        CALL OPF(STORE.FWD.QUEUE,F.STORE.FWD.QUEUE)

        FN.TAUT.STORE.JOURNAL = R.TAUT.PARAM<TAUT.PROCESSED.JNL>
        F.TAUT.STORE.JOURNAL  = ''
        CALL OPF(FN.TAUT.STORE.JOURNAL,F.TAUT.STORE.JOURNAL)

        Y.TGT.UPL.REPEAT = R.TAUT.PARAM<TAUT.TGT.UPL.REPEAT>
        FN.TGT.UPL.REPEAT = Y.TGT.UPL.REPEAT
        F.TGT.UPL.REPEAT = ""
        CALL OPF(FN.TGT.UPL.REPEAT,F.TGT.UPL.REPEAT)

        Y.TGT.UPL.FAILED  = R.TAUT.PARAM<TAUT.TGT.UPL.FAILED>
        FN.TGT.UPL.FAILED = Y.TGT.UPL.FAILED
        F.TGT.UPL.FAILED  = ""
        CALL OPF(FN.TGT.UPL.FAILED,F.TGT.UPL.FAILED)

        Y.TGT.UPLOAD.DONE = R.TAUT.PARAM<TAUT.TGT.UPLOAD.DONE>

        FN.TGT.UPLOAD.DONE = Y.TGT.UPLOAD.DONE
        F.TGT.UPLOAD.DONE = ""

        CALL OPF(FN.TGT.UPLOAD.DONE,F.TGT.UPLOAD.DONE)

        Y.TIME.DATE = TIMEDATE()
        CONVERT ":" TO "-" IN Y.TIME.DATE

        Y.TGT.UPL.FAILED.REPORT.ID = Y.TIME.DATE:"_Failed upload transaction.csv"
        Y.TGT.UPL.DONE.RPT.ID = Y.TIME.DATE:"_Processed transaction.csv"



    END
    SOURCE.FILE = R.TAUT.PARAM<TAUT.SOURCE.FILE>
    RETURN

LOAD.OFS.MESSAGE:
******************



    LOG.LEVEL = ''
    GOSUB READ.OFS.QUEUE
    SOURCE.OFS.ARR = R.STORE.FWD.QUEUE
    CONVERT ',' TO FM IN SOURCE.OFS.ARR
    SRC.OFS.MESSAGE = R.STORE.FWD.QUEUE
    OFS.APPLICATION.NAME = FIELD(OFS.QUEUE.ID,'_',2)
    Y.OPERATOR = FIELD(OFS.QUEUE.ID,'_',6)
    Y.TARGET.APPS:=OFS.APPLICATION.NAME:VM
    OPERATOR =  Y.OPERATOR
    Y.SOURCE.APP.ID = FIELD(OFS.QUEUE.ID,'_',3)

    Y.FUN = FIELD(OFS.QUEUE.ID,'_',4)

    FN.APP = "F.":OFS.APPLICATION.NAME
    F.APP = ""
    CALL OPF(FN.APP,F.APP)

    FN.APP.NAU = "F.":OFS.APPLICATION.NAME:"$NAU"
    F.APP.NAU = ""
    CALL OPF(FN.APP.NAU,F.APP.NAU)

    R.APP = ""
    Y.APP.NAU.ERR = ""
    Y.AMEND.FLAG = ""

    CALL F.READ(FN.APP.NAU,Y.SOURCE.APP.ID,R.APP,F.APP.NAU,Y.APP.NAU.ERR)
    IF R.APP EQ "" THEN
        Y.APP.ERR = ""
        CALL F.READ(FN.APP,Y.SOURCE.APP.ID,R.APP,F.APP,Y.APP.ERR)
    END
    IF R.APP AND Y.FUN = "I" THEN
        Y.AMEND.FLAG = 1

    END

    LOCATE OFS.APPLICATION.NAME IN R.TAUT.APPL.CHANGE.SYS<TAUT.APC.OB.APPLICATION,1> SETTING POS THEN
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'This Application has been made OB: ':OFS.APPLICATION.NAME
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    TR.OFS.SS.REC = ''
    CALL GET.STANDARD.SELECTION.DETS(OFS.APPLICATION.NAME,TR.OFS.SS.REC)

    TR.FIELD.NAME.LIST = TR.OFS.SS.REC<SSL.SYS.FIELD.NAME>

    IF OFS.APPLICATION.NAME = "LD.LOANS.AND.DEPOSITS" THEN
        TR.LDS.OFS.SS.REC = ''
        CALL GET.STANDARD.SELECTION.DETS("LD.SCHEDULE.DEFINE",TR.LDS.OFS.SS.REC)
        Y.TR.LDS.FIELD.NAME.LIST = TR.LDS.OFS.SS.REC<SSL.SYS.FIELD.NAME>
        TR.FIELD.NAME.LIST := VM:Y.TR.LDS.FIELD.NAME.LIST
    END
    TR.SYS.TYPE.LIST   = TR.OFS.SS.REC<SSL.SYS.TYPE>
    TR.SYS.VAL.PROG.LIST   = TR.OFS.SS.REC<SSL.SYS.VAL.PROG>

    R.TAUT.APPL.CHANGE = ''
    CALL F.READ(FN.TAUT.APPL.CHANGE,OFS.APPLICATION.NAME,R.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE,CHG.READ.ERR)

    RETURN

READ.OFS.QUEUE:
***************
    READ R.STORE.FWD.QUEUE FROM F.STORE.FWD.QUEUE,OFS.QUEUE.ID ELSE
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Cannot Find the STORE OFS QUEUE Record: ':OFS.QUEUE.ID
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    READ R.TAUT.OFS.TARGET FROM F.TAUT.STORE.JOURNAL,OFS.QUEUE.ID ELSE
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Cannot Find the JNL Record: ':OFS.QUEUE.ID
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END





    RETURN

MAIN.PROCESS:
*------------

    G.SPARE1 = OFS.QUEUE.ID
    CALL System.setVariable("MASTER.JNL.ID",OFS.QUEUE.ID)

    GOSUB SRC.OFS.FIELD.CHECK
    GOSUB TG.MANDATORY.FIELD.CHECK
    GOSUB CALL.OFS.BULK.MANAGER


    RETURN

SRC.OFS.FIELD.CHECK:
********************
    TARGET.FIELDS.OB = ''
    SRC.OFS.FIELD.NAME.ARR = ''
    TARGET.OFS.FIELD.NAME.ARR = ''
    Y.AA.FLAG = ''
    NO.FIELDS = DCOUNT(SRC.OFS.MESSAGE,',')
    TARGET.OFS = FIELDS(SRC.OFS.MESSAGE,',',1,4)
    Y.PROPERTY.CNT = 1
    Y.OLD.FIELD.CNT = ''
    Y.FIELD.VM.CNT = 1
    Y.FIELD.CNT = 0


    FOR IDX.NO = 5 TO NO.FIELDS

        Y.OLD.FIELD.CNT = Y.FIELD.VM.CNT
        LOC.VM.POS = 0
        Y.OFS.STR.VAL = FIELD(SRC.OFS.MESSAGE,',',IDX.NO)
        SRC.OFS.FIELD.NAME = FIELD(Y.OFS.STR.VAL,":",1)
        IF Y.AA.FLAG EQ '1' AND SRC.OFS.FIELD.NAME EQ 'FIELD.NAME' THEN
            Y.FIELD.NAME = FIELD(SRC.OFS.FIELD.NAME,":",1)
            Y.OFS.STR.VAL = FIELD(Y.OFS.STR.VAL,'=',1)
            Y.FIELD.VM.CNT = FIELD(Y.OFS.STR.VAL,":",3)
            SRC.OFS.FIELD.NAME = FIELD(FIELD(SRC.OFS.MESSAGE,',',IDX.NO),"=",2)
            Y.VAL = SRC.OFS.FIELD.NAME
            SRC.OFS.FIELD.NAME = FIELD(SRC.OFS.FIELD.NAME,':',1)
        END
        IF SRC.OFS.FIELD.NAME EQ 'FIELD.VALUE' THEN
            Y.SRC.OFS.FIELD.NAME = SRC.OFS.FIELD.NAME
            SRC.OFS.FIELD.NAME = FIELD(FIELD(SRC.OFS.MESSAGE,',',IDX.NO),"=",2)
            Y.VAL = SRC.OFS.FIELD.NAME
            R.AA.PROPERTY = ''
            TARGET.OFS := ',':Y.SRC.OFS.FIELD.NAME:':':Y.FIELD.PROP.CNT:':':Y.FIELD.VM.CNT:'=':Y.VAL
            CONTINUE
        END


        IF SRC.OFS.FIELD.NAME EQ 'PROPERTY' THEN
            Y.OFS.FIELD.NAME = SRC.OFS.FIELD.NAME
            Y.AA.FLAG = 1
            SRC.OFS.FIELD.NAME = FIELD(FIELD(SRC.OFS.MESSAGE,',',IDX.NO),"=",2)
            CALL F.READ(FN.AA.PROPERTY, SRC.OFS.FIELD.NAME, R.AA.PROPERTY, F.AA.PROPERTY, Y.ERR.PROP)
            IF NOT(R.AA.PROPERTY) OR Y.OLD.FIELD.CNT LE '0' THEN
                Y.PROPERTY.CNT--
            END
            Y.PROP.CLASS = R.AA.PROPERTY<AA.PROP.PROPERTY.CLASS>
            OFS.APPLICATION.NAME = 'AA.PRD.DES.':Y.PROP.CLASS
            TR.OFS.SS.REC = ''
            CALL GET.STANDARD.SELECTION.DETS(OFS.APPLICATION.NAME,TR.OFS.SS.REC)
            TR.FIELD.NAME.LIST = TR.OFS.SS.REC<SSL.SYS.FIELD.NAME>
            TR.SYS.TYPE.LIST   = TR.OFS.SS.REC<SSL.SYS.TYPE>
            TR.SYS.VAL.PROG.LIST   = TR.OFS.SS.REC<SSL.SYS.VAL.PROG>
            R.TAUT.APPL.CHANGE = ''
            CALL F.READ(FN.TAUT.APPL.CHANGE,OFS.APPLICATION.NAME,R.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE,CHG.READ.ERR)
            Y.TARGET.APPS:=OFS.APPLICATION.NAME:VM
            TARGET.OFS.FIELD.NAME.ARR<1,-1> = SRC.OFS.FIELD.NAME
            TARGET.OFS.PROP = Y.OFS.FIELD.NAME:':':Y.PROPERTY.CNT:'=':SRC.OFS.FIELD.NAME
            Y.FIELD.PROP.CNT = Y.PROPERTY.CNT
            Y.PROPERTY.CNT++
        END ELSE
            SRC.OFS.FIELD.NAME.ARR<-1> =  SRC.OFS.FIELD.NAME
            LOCATE SRC.OFS.FIELD.NAME IN TR.FIELD.NAME.LIST<1,1> SETTING LOC.VM.POS THEN
                TARGET.OFS.FIELD.NAME.ARR<1,-1> = SRC.OFS.FIELD.NAME
                IF Y.AA.FLAG EQ '1' THEN
                    IF R.AA.PROPERTY THEN
                        TARGET.OFS := ',':TARGET.OFS.PROP:',':Y.FIELD.NAME:':':Y.FIELD.PROP.CNT:':':Y.FIELD.VM.CNT:'=':Y.VAL
                    END ELSE
                        TARGET.OFS := ',':Y.FIELD.NAME:':':Y.FIELD.PROP.CNT:':':Y.FIELD.VM.CNT:'=':Y.VAL
                    END
                END ELSE
                    TARGET.OFS := ',':FIELD(SRC.OFS.MESSAGE,',',IDX.NO)
                END
            END ELSE
                TARGET.FIELDS.OB<1,-1> = SRC.OFS.FIELD.NAME
                IF Y.AA.FLAG EQ '1' THEN
                    Y.FIELD.VM.CNT--
                    IDX.NO++
                END
            END
        END
    NEXT IDX.NO

    IF TARGET.FIELDS.OB THEN
        LOG.LEVEL = 1         ;*Fatal
        TAUT.TEXT = 'Fields Missing in this Application (Might be Removed): ': OFS.APPLICATION.NAME
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    RETURN

TG.MANDATORY.FIELD.CHECK:
*************************
    IF OFS.APPLICATION.NAME MATCHES "LD.LOANS.AND.DEPOSITS":VM:"SWAP" THEN RETURN

    Y.APPL.CNT = DCOUNT(Y.TARGET.APPS,VM)
    I = 1
    LOOP
    WHILE I LE Y.APPL.CNT
        Y.APPL.NAME = Y.TARGET.APPS<1,I>
        CALL F.READ(FN.TAUT.APPL.CHANGE,Y.APPL.NAME,R.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE,CHG.READ.ERR)
        NO.OF.MD.FIELD = DCOUNT(R.TAUT.APPL.CHANGE<TAUT.APC.MANDATORY.FIELDS>,VM)
        FOR MPOS=1 TO NO.OF.MD.FIELD
            MD.FIELD.NAME = R.TAUT.APPL.CHANGE<TAUT.APC.MANDATORY.FIELDS,MPOS>
            LOCATE MD.FIELD.NAME IN SRC.OFS.FIELD.NAME.ARR SETTING MFOUND.POS ELSE
                TARGET.OFS := ',': MD.FIELD.NAME :"::=":R.TAUT.APPL.CHANGE<TAUT.APC.FIELD.VALUE,MPOS>
            END
        NEXT MPOS
        I++
    REPEAT
    RETURN

CALL.OFS.BULK.MANAGER:
**********************
    theResponses = ""

    IF TARGET.OFS THEN

        SAVE.APP = APPLICATION
        SAVE.V = V
        OFS.SRC.ID.BACKUP = OFS$SOURCE.ID
        OFS.SRC.REC.BACKUP = OFS$SOURCE.REC

        OFS$SOURCE.ID = OFS.SRC.ID
        OFS$SOURCE.REC = R.OFS.SOURCE
        Y.APPL = FIELD(TARGET.OFS,',',1)
        Y.AA.INP.FLAG = ''


        IF Y.AMEND.FLAG THEN
            NO.OF.FIELDS = DCOUNT(TARGET.OFS,',')
            TARGET.OFS = FIELDS(TARGET.OFS,',',1,3):",":Y.SOURCE.APP.ID:FIELDS(TARGET.OFS,",",4,NO.OF.FIELDS)
        END

        IF Y.FUN EQ 'I' THEN
            Y.AA.INP.FLAG = 1
            Y.SOURCE.APP.ID = FIELD(OFS.QUEUE.ID,'_',3)
            CALL F.READ(FN.TAUT.W.NEW.ID.REF, Y.SOURCE.APP.ID, R.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF, Y.TAUT.ERR)
        END
        IF Y.FUN EQ 'A' OR Y.FUN EQ 'D' OR Y.FUN EQ 'R' THEN

            Y.SOURCE.APP.ID = FIELD(OFS.QUEUE.ID,'_',3)
            CALL F.READ(FN.TAUT.W.NEW.ID.REF, Y.SOURCE.APP.ID, R.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF, Y.TAUT.ERR)
            IF R.TAUT.W.NEW.ID.REF THEN
                TARGET.OFS:=R.TAUT.W.NEW.ID.REF
            END ELSE
                TAUT.TEXT = 'OFS Message Failed' :" * ": TARGET.OFS
                GOSUB UPDATE.TAUT.LOG
                GOSUB UPDATE.TGT.UPL.REPEAT
                Y.FAILED.REASON = "Target application id missing in concat file"
                GOSUB UPDATE.FAILED.REPORT
                RETURN
            END
        END

        IF Y.SOURCE.APP.ID[1,2] EQ "AA" THEN
            SAVE.APP = APPLICATION
            SAVE.V = V
            OFS.SRC.ID.BACKUP = OFS$SOURCE.ID
            OFS.SRC.REC.BACKUP = OFS$SOURCE.REC

            OFS$SOURCE.ID = OFS.SRC.ID
            OFS$SOURCE.REC = R.OFS.SOURCE

            theRequests = TARGET.OFS
            theResponses = ''
            requestCommitted = ''
            CALL OFS.BULK.MANAGER(theRequests, theResponses, requestCommitted)
            APPLICATION = SAVE.APP
            V = SAVE.V
            OFS$SOURCE.ID  = OFS.SRC.ID.BACKUP
            OFS$SOURCE.REC = OFS.SRC.REC.BACKUP
        END ELSE

            Y.OFS.MSG = TARGET.OFS
            CALL OFS.GLOBUS.MANAGER(OFS.SRC.ID,Y.OFS.MSG)
            theResponses = Y.OFS.MSG
        END





        Y.OFS.MSG.ERROR.PART = theResponses['/',3,1]
        ERROR.FLAG = Y.OFS.MSG.ERROR.PART[',',1,1]
        OFS.TXN.ID   = FIELD(theResponses,'/',1)

        IF Y.AA.INP.FLAG EQ '1' THEN
            Y.AAA.ID.REF = theResponses['/',1,1]
            Y.AAA.ID = Y.AAA.ID.REF[20,20]
            R.TAUT.W.NEW.ID.REF = Y.AAA.ID
            CALL F.WRITE(FN.TAUT.W.NEW.ID.REF, Y.AAA.REF.ID, R.TAUT.W.NEW.ID.REF)
        END

*        IF requestCommitted NE 1 OR ERROR.FLAG = -1 THEN
        IF ERROR.FLAG = -1 THEN
            TAUT.TEXT = 'OFS Message Failed' :" * ": TARGET.OFS
            GOSUB UPDATE.TAUT.LOG
            GOSUB UPDATE.TGT.UPL.REPEAT
            GOSUB UPDATE.FAILED.REPORT
        END ELSE
            IF Y.AA.INP.FLAG EQ '1' THEN
                Y.AAA.ID.REF = theResponses['/',1,1]
*            Y.AAA.ID = Y.AAA.ID.REF[20,20]

                R.TAUT.W.NEW.ID.REF = ""
                Y.TAUT.W.NEW.ID.REF.ERR = ""
                CALL F.READ(FN.TAUT.W.NEW.ID.REF,Y.SOURCE.APP.ID,R.TAUT.W.NEW.ID.REF,F.TAUT.W.NEW.ID.REF,Y.TAUT.W.NEW.ID.REF.ERR)
                IF R.TAUT.W.NEW.ID.REF EQ "" THEN
                    R.TAUT.W.NEW.ID.REF = Y.AAA.ID.REF
*                    WRITE R.TAUT.W.NEW.ID.REF TO F.TAUT.W.NEW.ID.REF,Y.SOURCE.APP.ID
                    CALL F.WRITE(FN.TAUT.W.NEW.ID.REF,R.TAUT.W.NEW.ID.REF,Y.SOURCE.APP.ID)
                END
            END
            IF R.TAUT.UPLD.DONE.RPT EQ "" THEN
                R.TAUT.UPLD.DONE.RPT = "Application":Y.DELIM:"Function":Y.DELIM:"Source Transaction ID":Y.DELIM:"Target Transaction ID"
                R.TAUT.UPLD.DONE.RPT := FM:OFS.APPLICATION.NAME:Y.DELIM:Y.FUN:Y.DELIM:Y.SOURCE.APP.ID:Y.DELIM:R.TAUT.W.NEW.ID.REF
            END ELSE
                R.TAUT.UPLD.DONE.RPT<-1> = OFS.APPLICATION.NAME:Y.DELIM:Y.FUN:Y.DELIM:Y.SOURCE.APP.ID:Y.DELIM:R.TAUT.W.NEW.ID.REF
            END
            WRITE SRC.OFS.MESSAGE TO F.TGT.UPLOAD.DONE,OFS.QUEUE.ID
            DELETE F.STORE.FWD.QUEUE,OFS.QUEUE.ID
        END

    END
    CALL JOURNAL.UPDATE("TAUT.OFS.UPLOAD.TARGET")
    RETURN

UPDATE.TAUT.LOG:
****************
    YID = ''
    CALL ALLOCATE.UNIQUE.TIME(YID)
    IF TAUT.TEXT THEN
        IF T.LOG.LEVEL EQ 'FULL' THEN
            BEGIN CASE
            CASE LOG.LEVEL = 1
                TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 2
                TAUT.TEXT = "Warning  :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 3
                TAUT.TEXT = "Info :" :TIMEDATE():" ":TAUT.TEXT
            END CASE

            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END

        IF T.LOG.LEVEL EQ 'FATAL' AND LOG.LEVEL EQ 1 THEN
            TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END
    END

    TAUT.TEXT = ''
    RETURN


UPDATE.TGT.UPL.REPEAT:
**********************
* Move the failed cases to TGT.UPL.REPEAT folder to reprocess and delete the message from STORE.FWD.QUEUE.

    WRITE SRC.OFS.MESSAGE TO F.TGT.UPL.REPEAT,OFS.QUEUE.ID
    WRITE SRC.OFS.MESSAGE TO F.TGT.UPL.FAILED,OFS.QUEUE.ID
    DELETE F.STORE.FWD.QUEUE,OFS.QUEUE.ID
    RETURN

UPDATE.FAILED.REPORT:
*********************
    Y.APPL.NAME = ""
    Y.APPL.NAME = OFS.APPLICATION.NAME
    Y.OFS.ID = OFS.QUEUE.ID
    Y.APP.ID = OFS.TXN.ID
    Y.ORD.ID = FIELD(theResponses,"/",2,1)
    IF Y.FAILED.REASON EQ "" THEN
        Y.FAILED.REASON = FIELD(theResponses,",",2,1)
    END

    IF R.FAILED.REPORT = "" THEN
        R.FAILED.REPORT = "Application name":Y.DELIM:"OFS id":Y.DELIM:"Target Application ID":Y.DELIM:"ORD ID":Y.DELIM:"Failed Reason"
        R.FAILED.REPORT<-1> = Y.APPL.NAME:Y.DELIM:Y.OFS.ID:Y.DELIM:Y.APP.ID:Y.DELIM:Y.ORD.ID:Y.DELIM:Y.FAILED.REASON
    END ELSE
        R.FAILED.REPORT<-1> = Y.APPL.NAME:Y.DELIM:Y.OFS.ID:Y.DELIM:Y.APP.ID:Y.DELIM:Y.ORD.ID:Y.DELIM:Y.FAILED.REASON
    END

    RETURN
END



